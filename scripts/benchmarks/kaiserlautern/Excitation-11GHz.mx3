//Define basic properties of mesh and other basic parameters of simulation
Nx:=4096 //Number of cells in x
Ny:=32  //       -"-         y
Nz:=1    //       -"-         z
Length_x:=20.0e-6 //size of simulated area in x
Length_y:=0.1e-6  //size of simulated area in y
Length_z:=40.0e-9 //size of simulated area in z
MaxDt = 1e-9 //maximum timestep - sometimes needed for long simulations without dynamics in a certain time-window
MaxErr = 1e-8//maximum error, has to be low for YIG
//FixDt = 1e-13 // fix the timestep, needed for instance in simulations with temperature
//Dind = 0.00035/2 //effective strength of interfacial DMI
//MaxErr = 1e-6 //Maximum error
//ext_EnableUnsafe() //In theory should enable space-dependent DMI but that does not work so great
//OutputFormat = OVF2_TEXT //save in text format for current analysis in Python and Matlab

//Define magnetic field via the two angles theta and phi (theta: in-plane, origin at x-axis, phi: out-of-plane, origin in-plane)
Bext := 0.20 //Field in T
theta := 0.0
phi := 0.0
Bx:=Bext*cos(theta)*cos(phi)
By:=Bext*sin(theta)*cos(phi)
Bz:=Bext*sin(phi)

//Define position of antenna
antenna_pos:=Length_x/2

//Define CellSize
Cx:=Length_x/Nx
Cy:=Length_y/Ny
Cz:=Length_z/Nz

SetGridsize(Nx, Ny, Nz)
//SetPBC(5, 0, 0)
SetCellsize(Length_x/Nx, Length_y/Ny, Length_z/Nz)

// Def. geometry:
setgeom(rect(20.0e-6, 0.1e-6))
saveas(geom, "waveguide")
snapshot(geom)

// define material parameters
Msat  = 1.4e5 //saturation magnetization
Aex   = 3.5-12 //exchange constant
//anisU   = vector(0, 0, 1) //anisotropy axis for unaxial anisotropy
//Ku1 =  0.0 //(out of-plane) anisotropy constant - Ku1 = Msat*Bk/2 
alpha = 0.0002 //global alpha

defregion(200,rect(Length_x,Length_y))
Msat.setregion(200,1.4e5)
Aex.setregion(200,3.5e-12)
alpha.setregion(200,2e-4)

//increase of alpha towards edges
number_of_steps:=25 //number of alpha steps
width_of_step:=2*Cx //width of the areas with fixed alpha
// Def. region of in center where effective field is saved to table:
DefRegion(255, rect(5*Cx, 5*Cy))
Msat.setregion(255,1.4e5)
Aex.setregion(255,3.5e-12)
alpha.setregion(255,2e-4)

for i:=1; i<number_of_steps; i++{
	//Define the regions in which the dampin should be increased
	DefRegion(i, rect(width_of_step, Length_y).Transl(-Length_x*0.5+(i-1)*width_of_step,0,0))
	DefRegion(i+number_of_steps, rect(width_of_step, Length_y).Transl(Length_x*0.5-(i-1)*width_of_step,0,0))
	
	//The damping should decrease exponentially from a start value alpha_0 to a selected value alpha_end
	//alpha(region)=alpha_0*exp(-beta*x)
	alpha_end:=0.0002
	alpha_0:=0.5
	beta:=-log(alpha_end/alpha_0)/number_of_steps
	
	//Define the damping values in each region
	alpha.setRegion(i,alpha_0*exp(-beta*i))
	alpha.setRegion(i+number_of_steps,alpha_0*exp(-beta*i))
	Msat.setRegion(i,1.4e5)
	Msat.setRegion(i+number_of_steps,1.4e5)
	Aex.setRegion(i,3.5e-12)
	Aex.setRegion(i+number_of_steps,3.5e-12)
}

snapshot(regions)
snapshot(alpha)
snapshot(Msat)
snapshot(Aex)

// load ground state relaxation 
//m = uniform(0,0,1)
m.loadfile("D:/Simulationen/Testskripte/GS-200,0,0.ovf")
//set field
B_ext = vector(Bx, By, Bz)
//set timestep for saving in the table
tableautosave(1e-12)

//start excitation def
fmax:= 20.0e9 //maximum resolvable frequency

//excitation frequency
f:=11.0e9

//start of antenna pulse
t0:=1e-9

// pulse duration antenna
delta_t:=10.0e-9

a:=25.0e-9 //half width of antenna in nm
b:=25.0e-9 //half height of antenna in nm
y_pump := -25.0e-9 //position of antenna field in nm
current := 1.25e-3 //current in mA
position_ant := 10.0e-6
d := 150.0e-9 //center-to-center-seperation of antennas
ant_field_l:= newVectorMask(Nx, 1, 1) //field of left antenna
for i:=0; i<Nx; i++{
	for j:=0; j<1; j++{ 
		HF1:=a-i*Cx+position_ant+d
		HF2:=a+i*Cx-position_ant-d
		HF3:=b-y_pump
		HF4:=b+y_pump
		PREFACTOR:=-0.5*current*(4*pi*1e-7)/(8*pi*a*b) //current*mu0/8*pi*a*b
		
		Bx:=PREFACTOR*(HF1*0.5*log((pow(HF3,2)+pow(HF1,2))/(pow(HF4,2)+pow(HF1,2)))+HF2*0.5*log((pow(HF3,2)+pow(HF2,2))/(pow(HF4,2)+pow(HF2,2)))+HF3*atan(HF1/HF3)+HF4*atan(-HF1/HF4)-HF3*atan(-HF2/HF3)-HF4*atan(HF2/HF4))	
		Bz:=PREFACTOR*(HF3*0.5*log((pow(HF3,2)+pow(HF1,2))/(pow(HF2,2)+pow(HF3,2)))+HF1*atan(HF3/HF1)+HF2*atan(-HF3/HF2)+HF4*0.5*log((pow(HF1,2)+pow(HF4,2))/(pow(HF2,2)+pow(HF4,2)))-HF1*atan(-HF4/HF1)-HF2*atan(HF4/HF2))

		//Set the external field in every cell i,j to be the calculated antenna field.
		ant_field_l.setVector(i, j, 0, vector(Bx, 0, Bz))
	}
}

ant_field_r:= newVectorMask(Nx, 1, 1) //field of right antenna
for i:=0; i<Nx; i++{
	for j:=0; j<1; j++{
		HF1:=a-i*Cx+position_ant-d
		HF2:=a+i*Cx-position_ant+d
		HF3:=b-y_pump
		HF4:=b+y_pump
		PREFACTOR:=-0.5*current*(4*pi*1e-7)/(8*pi*a*b) //current*mu0/8*pi*a*b
		
		Bx:=PREFACTOR*(HF1*0.5*log((pow(HF3,2)+pow(HF1,2))/(pow(HF4,2)+pow(HF1,2)))+HF2*0.5*log((pow(HF3,2)+pow(HF2,2))/(pow(HF4,2)+pow(HF2,2)))+HF3*atan(HF1/HF3)+HF4*atan(-HF1/HF4)-HF3*atan(-HF2/HF3)-HF4*atan(HF2/HF4))	
		Bz:=PREFACTOR*(HF3*0.5*log((pow(HF3,2)+pow(HF1,2))/(pow(HF2,2)+pow(HF3,2)))+HF1*atan(HF3/HF1)+HF2*atan(-HF3/HF2)+HF4*0.5*log((pow(HF1,2)+pow(HF4,2))/(pow(HF2,2)+pow(HF4,2)))-HF1*atan(-HF4/HF1)-HF2*atan(HF4/HF2))

		//Set the external field in every cell i,j to be the calculated antenna field.
		ant_field_r.setVector(i, j, 0, vector(Bx, 0, Bz))
	}
}

ant_field_c:= newVectorMask(Nx, 1, 1) //field of right antenna
for i:=0; i<Nx; i++{
	for j:=0; j<1; j++{ 
		HF1:=a-i*Cx+position_ant
		HF2:=a+i*Cx-position_ant
		HF3:=b-y_pump
		HF4:=b+y_pump
		PREFACTOR:=current*(4*pi*1e-7)/(8*pi*a*b) //current*mu0/8*pi*a*b
		
		Bx:=PREFACTOR*(HF1*0.5*log((pow(HF3,2)+pow(HF1,2))/(pow(HF4,2)+pow(HF1,2)))+HF2*0.5*log((pow(HF3,2)+pow(HF2,2))/(pow(HF4,2)+pow(HF2,2)))+HF3*atan(HF1/HF3)+HF4*atan(-HF1/HF4)-HF3*atan(-HF2/HF3)-HF4*atan(HF2/HF4))	
		Bz:=PREFACTOR*(HF3*0.5*log((pow(HF3,2)+pow(HF1,2))/(pow(HF2,2)+pow(HF3,2)))+HF1*atan(HF3/HF1)+HF2*atan(-HF3/HF2)+HF4*0.5*log((pow(HF1,2)+pow(HF4,2))/(pow(HF2,2)+pow(HF4,2)))-HF1*atan(-HF4/HF1)-HF2*atan(HF4/HF2))

		//Set the external field in every cell i,j to be the calculated antenna field.
		ant_field_c.setVector(i, j, 0, vector(Bx, 0, Bz))
	}
}

f_rise := 5.0 //Rise frequency in GHz

B_ext.Add(ant_field_l,(tanh(2*f_rise*(t-t0)/1.0e-9) *tanh(2*f_rise*(t0+delta_t-t)/1.0e-9) +1)*0.5*sin(2*pi*f*t))
B_ext.Add(ant_field_r,(tanh(2*f_rise*(t-t0)/1.0e-9) *tanh(2*f_rise*(t0+delta_t-t)/1.0e-9) +1)*0.5*sin(2*pi*f*t))
B_ext.Add(ant_field_c,(tanh(2*f_rise*(t-t0)/1.0e-9) *tanh(2*f_rise*(t0+delta_t-t)/1.0e-9) +1)*0.5*sin(2*pi*f*t))
autosave(m.comp(2),1/(2*fmax)) //save m_z every n seconds
tableAdd(B_eff.Region(number_of_steps+1))
tableAdd(B_ext.Region(number_of_steps+1))
snapshot(B_ext.comp(2))
run(100e-9)





